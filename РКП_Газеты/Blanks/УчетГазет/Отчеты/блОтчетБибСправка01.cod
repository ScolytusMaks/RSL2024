Class inherited СИС2.БазовыйБланк "Библиографическая справка" ;

--{{ 2_Свойства

InClass private

  stored var НастройкаВыполнена :Logical;

  stored var Фл_NNN :Integer;                 stored var ШР_NNN :Integer;
  stored var Фл_ПРЕКР :Integer;               stored var ШР_ПРЕКР :Integer;
  stored var Фл_N_ГОСРЕГ :Integer;            stored var ШР_N_ГОСРЕГ :Integer;
  stored var Фл_ISSN :Integer;                stored var ШР_ISSN :Integer;
  stored var Фл_IdInLibrarySystem :Integer;   stored var ШР_IdInLibrarySystem :Integer;
  stored var Фл_IdInUSSRNewspapers :Integer;  stored var ШР_IdInUSSRNewspapers :Integer;
  stored var Фл_НАЗВ :Integer;                stored var ШР_НАЗВ :Integer;
  stored var Фл_НАЗВ_ИН :Integer;             stored var ШР_НАЗВ_ИН :Integer;
  stored var Фл_НАЗВ_НАЦ :Integer;            stored var ШР_НАЗВ_НАЦ :Integer;
  stored var Фл_ПОДЗАГ :Integer;              stored var ШР_ПОДЗАГ :Integer;
  stored var Фл_ТГ :Integer;                  stored var ШР_ТГ :Integer;
  stored var Фл_ГЕО :Integer;                 stored var ШР_ГЕО :Integer;
  stored var Фл_РЕД :Integer;                 stored var ШР_РЕД :Integer;
  stored var Фл_ТФ :Integer;                  stored var ШР_ТФ :Integer;
  stored var Фл_EMAIL :Integer;               stored var ШР_EMAIL :Integer;
  stored var Фл_ТИ :Integer;                  stored var ШР_ТИ :Integer;
  stored var Фл_ТЕМ :Integer;                 stored var ШР_ТЕМ :Integer;
  stored var Фл_ЯЗ :Integer;                  stored var ШР_ЯЗ :Integer;
  stored var Фл_УЧР :Integer;                 stored var ШР_УЧР :Integer;
  stored var Фл_Дата1 :Integer;               stored var ШР_Дата1 :Integer;
  stored var Фл_РЕДАКТОР :Integer;            stored var ШР_РЕДАКТОР :Integer;
  stored var Фл_ГОД_РЕГ :Integer;             stored var ШР_ГОД_РЕГ :Integer;
  stored var Фл_ПЕРИОДИЧН: Integer;           stored var ШР_ПЕРИОДИЧН :Integer;
  stored var Фл_СР_РАЗ_ТИРАЖ :Integer;        stored var ШР_СР_РАЗ_ТИРАЖ :Integer;
  stored var Фл_ЛППИ :Integer;                stored var ШР_ЛППИ :Integer;
  stored var Фл_ПослНом :Integer;             stored var ШР_ПослНом :Integer;

InObject private

  --{ запрос --
  фреймУсловияОтбора :TemplateFrame;
  секцУсловияОтбора :Section;
  стркУО_МестоИздания :TemplateRow;
  стркУО_ТипИздания :TemplateRow;
  стркУО_Периодичность :TemplateRow;
  стркУО_ТерриторияРаспространения1 :TemplateRow;
  стркУО_ТерриторияРаспространения2 :TemplateRow;
  стркУО_Тематика :TemplateRow;
  стркУО_ЧАЦН1 :TemplateRow;
  стркУО_ЧАЦН2 :TemplateRow;
  стркУО_Язык :TemplateRow;
  стркУО_ГодНачалаИздания :TemplateRow;
  стркУО_ГодЛППИ :TemplateRow;
  стркУО_ГодВыпуска :TemplateRow;
  --}

  --{ настройка --
  фреймНастройкаСтолбцов :TemplateFrame;
  кнНастройка :Button;
  ПоказатьНастройку :Logical;
  МестоИздания :РКП_Справочники.спрГеография;
  ТипИздания :Integer;
  Периодичность :String;
  ТерриторияРаспространения :РКП_Справочники.спрТерриторияРаспространения;
  Тематика :РКП_Справочники.спрТематика;
  ЧАЦН :РКП_Справочники.спрТематика;
  Язык :РКП_Справочники.спрЯзыкИздания;
  ГодНачалаИздания :Integer;
  ГодЛППИ :Integer;
  ГодВыпуска :Integer;
  --}

  --{ отчет (объекты) --
  фреймРезультат :TemplateFrame;
  секцШапкаСПР :Section;
  секцТелоСПР :Section;
  --}

  --{ отчет (данные) --
  NNN                :Integer[];
  ПРЕКР              :Logical[];
  N_ГОСРЕГ           :String[];
  ISSN               :String[];
  IdInLibrarySystem  :String[];
  IdInUSSRNewspapers :String[];
  НАЗВ               :String[];
  НАЗВ_ИН            :String[];
  НАЗВ_НАЦ           :String[];
  ПОДЗАГ             :String[];
  ТГ                 :РКП_Справочники.СпрТерриторияРаспространения[];
  ГЕО                :РКП_Справочники.СпрГеография[];
  РЕД                :РКП_Справочники.спрРедакция[];
  ТФ                 :String[];
  EMAIL              :String[];
  ТИ                 :Integer[];
  ТЕМ                :РКП_Справочники.спрТематика[];
  ЯЗ                 :РКП_Справочники.СпрЯзыкИздания[];
  УЧР                :String[];
  Дата1              :String[];
  РЕДАКТОР           :String[];
  ГОД_РЕГ            :Integer[];
  ПЕРИОДИЧН          :String[];
  СР_РАЗ_ТИРАЖ       :Numeric[];
  ЛППИ               :String[];
  ПослНом            :String[];
  Газета             :РКП_Газеты.Газета[];
  --}

--}}

-- 2_Обработчики событий шаблона0. --

  proc шаблон_ПриОткрытии(Create :Logical);
    if (not НастройкаВыполнена) then
      НастройкаСтолбцовПоУмолчанию;
      НастройкаВыполнена = true;
    fi;
    фреймУсловияОтбора.Visible = true;
    фреймНастройкаСтолбцов.Visible = false;
    ВидСекцииУсловияотбора;
    ОформитьОтчет;
  end;

  proc шаблон_ПриЗакрытии(Destroy :Logical);
    inherited шаблон_ПриЗакрытии(Destroy);
  end;

-- 2_Обработчики событий клеток шаблона0. --

  func ПолеФл_ПриНажатии(Cell :TemplateCell; Action :Template.ClickTypes) :Logical;
    var vTest :Integer;
    vTest = Self.GetClassField(Cell.Contents) as Integer;
    if (vTest in [0,-1]) then vTest = 1;
    else vTest = -1;
    fi;
    Self.SetClassField(Cell.Contents, vTest);
  end;

  func ПолеФл_ПриВыводе(Cell :TemplateCell; Value :Variant; Action :Template.OutputTypes; var Format :String) :Variant;
    var vTest :Integer;
    vTest = Self.GetClassField(Cell.Contents) as Integer;
    Result = (vTest = 1);
  end;

  func ПолеНастройка_ПриПодсказке(Cell :TemplateCell; var Text :String) :Logical;
    var vTxt :String;
    var p :Integer;
    vTxt = Cell.Contents;
    p = Pos('..', vTxt) - 1;
    Text = SubStr(vTxt, 1, p);
  end;

  func ОтрытьГазету(Cell :TemplateCell; Action :Template.ClickTypes) :Logical;
    var Index :Integer;
    var UForm :РКП_Газеты.УчетГазет.редГазета;
    Index = Cell.Frame;
    UForm = РКП_Газеты.УчетГазет.редГазета.Create;
    if (Газета[Index] <> nil) then
      UForm.ShowEx(Газета[Index], Window.ModalWindow);
    fi;
  end;

-- 2_Обработчики событий прочих объектов шаблона0. --

  proc КнНастройка_ПриНажатии(Sender :Button);
    фреймУсловияОтбора.Visible     = ПоказатьНастройку;
    фреймНастройкаСтолбцов.Visible = not ПоказатьНастройку;
    фреймРезультат.Visible         = ПоказатьНастройку;
    ПоказатьНастройку = not ПоказатьНастройку;
    if ПоказатьНастройку then
      ОформитьОтчет;
    fi;
  end;

  proc КнРасчет_ПриНажатии(Sender :Button);
    var локФорма :UserObject;
    локФорма  = РКП_Газеты.УчетГазет.Запросы.блОтборГазетСпр1.Create;
    if CmOk = локФорма.Show(Window.ModalWindow) then
      МестоИздания              = локФорма.МестоИздания;
      ТипИздания                = локФорма.ТипИздания;
      Периодичность             = локФорма.Периодичность;
      ТерриторияРаспространения = локФорма.ТерриторияРаспространения;
      Тематика                  = локФорма.Тематика;
      ЧАЦН                      = локФорма.ЧАЦН;
      Язык                      = локФорма.Язык;
      ГодНачалаИздания          = локФорма.ГодНачалаИздания;
      ГодЛППИ                   = локФорма.ГодЛППИ;
      ГодВыпуска                = локФорма.ГодВыпуска;
      ПостроениеОтчета(локФорма.ПолучитьУсловиеОтбораГазет);
      ПоказатьНастройку = true;
      КнНастройка_ПриНажатии(кнНастройка);
      кнНастройка.State = false;
      ОформитьОтчет;
    fi;
    ВидСекцииУсловияотбора;
  end;

  func КнВосстановитьНастройкуСтолбцов(Cell :TemplateCell;Action :Template.ClickTypes) :Logical;
    НастройкаСтолбцовПоУмолчанию;
    Result = False;
  end;

-- 2_Вспомогательные методы0. --

  proc НастройкаСтолбцовПоУмолчанию;
    Фл_NNN                = 1; ШР_NNN                = 10;
    Фл_ПРЕКР              = 0; ШР_ПРЕКР              = 15;
    Фл_N_ГОСРЕГ           = 1; ШР_N_ГОСРЕГ           = 30;
    Фл_ISSN               = 0; ШР_ISSN               = 20;
    Фл_IdInLibrarySystem  = 0; ШР_IdInLibrarySystem  = 20;
    Фл_IdInUSSRNewspapers = 0; ШР_IdInUSSRNewspapers = 20;
    Фл_НАЗВ               = 1; ШР_НАЗВ               = 80;
    Фл_НАЗВ_ИН            = 0; ШР_НАЗВ_ИН            = 40;
    Фл_НАЗВ_НАЦ           = 0; ШР_НАЗВ_НАЦ           = 40;
    Фл_ПОДЗАГ             = 1; ШР_ПОДЗАГ             = 60;
    Фл_ТГ                 = 0; ШР_ТГ                 = 30;
    Фл_ГЕО                = 0; ШР_ГЕО                = 40;
    Фл_РЕД                = 1; ШР_РЕД                = 70;
    Фл_ТФ                 = 1; ШР_ТФ                 = 45;
    Фл_EMAIL              = 0; ШР_EMAIL              = 40;
    Фл_ТИ                 = 0; ШР_ТИ                 = 20;
    Фл_ТЕМ                = 0; ШР_ТЕМ                = 70;
    Фл_ЯЗ                 = 0; ШР_ЯЗ                 = 30;
    Фл_УЧР                = 0; ШР_УЧР                = 40;
    Фл_Дата1              = 0; ШР_Дата1              = 18;
    Фл_РЕДАКТОР           = 1; ШР_РЕДАКТОР           = 40;
    Фл_ГОД_РЕГ            = 0; ШР_ГОД_РЕГ            = 20;
    Фл_ПЕРИОДИЧН          = 0; ШР_ПЕРИОДИЧН          = 30;
    Фл_СР_РАЗ_ТИРАЖ       = 0; ШР_СР_РАЗ_ТИРАЖ       = 30;
    Фл_ЛППИ               = 0; ШР_ЛППИ               = 15;
    Фл_ПослНом            = 0; ШР_ПослНом            = 25;
  end;

  proc ВидСекцииУсловияотбора;
    стркУО_МестоИздания.Visible = not (МестоИздания = nil);
    стркУО_ТипИздания.Visible = not (ТипИздания = nil);
    стркУО_Периодичность.Visible = not (Периодичность in [nil, 'Все']);
    стркУО_ТерриторияРаспространения1.Visible = not (ТерриторияРаспространения = nil);
    стркУО_ТерриторияРаспространения2.Visible = not (ТерриторияРаспространения = nil);
    стркУО_Тематика.Visible = not (Тематика = nil);
    стркУО_ЧАЦН1.Visible = not (ЧАЦН = nil);
    стркУО_ЧАЦН2.Visible = not (ЧАЦН = nil);
    стркУО_Язык.Visible = not (Язык = nil);
    стркУО_ГодНачалаИздания.Visible = not (ГодНачалаИздания = nil);
    стркУО_ГодЛППИ.Visible = not (ГодЛППИ = nil);
    стркУО_ГодВыпуска.Visible = not (ГодВыпуска = nil);
  end;

  proc ПостроениеОтчета(locNspFilter :String);
    var k, kk :Integer;
    var rNsp :РКП_Газеты.Газета;
    ОчиститьРезультат;
    with Query.Create([РКП_Газеты.Газета]) do
      Order = 'НазваниеОсн';
      Filter = locNspFilter;
      Select;
      kk = Count;
      while not Eof do
        k = k + 1;
        Hint('Выборка данных...', k, kk);
        rNsp = Current as РКП_Газеты.Газета;
        NNN[k] = k;
        Газета[k] = rNsp;
        if (Фл_ПРЕКР = 1)              then ПРЕКР[k]              = rNsp.Прекращено;                fi;
        if (Фл_N_ГОСРЕГ = 1)           then N_ГОСРЕГ[k]           = rNsp.РегНомер;                  fi;
        if (Фл_ISSN = 1)               then ISSN[k]               = rNsp.ISSN;                      fi;
        if (Фл_IdInLibrarySystem = 1)  then IdInLibrarySystem[k]  = rNsp.IdInLibrarySystem;         fi;
        if (Фл_IdInUSSRNewspapers = 1) then IdInUSSRNewspapers[k] = rNsp.IdInUSSRNewspapers;        fi;
        if (Фл_НАЗВ = 1)               then НАЗВ[k]               = rNsp.НазваниеОсн;               fi;
        if (Фл_НАЗВ_ИН = 1)            then НАЗВ_ИН[k]            = rNsp.НазваниеИнЯз;              fi;
        if (Фл_НАЗВ_НАЦ = 1)           then НАЗВ_НАЦ[k]           = rNsp.НазваниеНацЯз;             fi;
        if (Фл_ПОДЗАГ = 1)             then ПОДЗАГ[k]             = rNsp.Подзаголовок;              fi;
        if (Фл_ТГ = 1)                 then ТГ[k]                 = rNsp.ТерриторияРаспространения; fi;
        if (Фл_ГЕО = 1)                then ГЕО[k]                = Получить_ГЕО(rNsp);             fi;
        if (Фл_РЕД = 1)                then РЕД[k]                = rNsp.Редакция;                  fi;
        if (Фл_ТФ = 1)                 then ТФ[k]                 = rNsp.Телефон;                   fi;
        if (Фл_EMAIL = 1)              then EMAIL[k]              = rNsp.E_mail;                    fi;
        if (Фл_ТИ = 1)                 then ТИ[k]                 = rNsp.ТипИздания;                fi;
        if (Фл_ТЕМ = 1)                then ТЕМ[k]                = Получить_ТЕМ(rNsp);             fi;
        if (Фл_ЯЗ = 1)                 then ЯЗ[k]                 = Получить_ЯЗ(rNsp);              fi;
        if (Фл_УЧР = 1)                then УЧР[k]                = rNsp.Учред;                     fi;
        if (Фл_Дата1 = 1)              then Дата1[k]              = Получить_Дата1(rNsp);           fi;
        if (Фл_РЕДАКТОР = 1)           then РЕДАКТОР[k]           = rNsp.РедакторГазеты;            fi;
        if (Фл_ГОД_РЕГ = 1)            then ГОД_РЕГ[k]            = rNsp.ГодЛетописи;               fi;
        if (Фл_ПЕРИОДИЧН = 1)          then ПЕРИОДИЧН[k]          = rNsp.Периодичность;             fi;
        if (Фл_СР_РАЗ_ТИРАЖ = 1)       then СР_РАЗ_ТИРАЖ[k]       = Получить_СрРазТираж(rNsp);      fi;
        if (Фл_ЛППИ = 1)               then ЛППИ[k]               = Получить_ЛППИ(rNsp);            fi;
        if (Фл_ПослНом = 1)            then ПослНом[k]            = Получить_ПослНом(rNsp);         fi;
        Next;
      od;
    end;
    секцТелоСПР.Count = kk;
  end;

  proc ОчиститьРезультат;
    var i :Integer;
    var cDataName :String;
    for i = 1 .. секцТелоСПР.ColumnsCount do
      if секцТелоСПР.Column[i].Printed then
        cDataName = секцТелоСПР.Cell[i, 1].Contents;
        if (cDataName <> nil) then
          if Pos('.', cDataName) > 0 then
            cDataName = ExtractWord(cDataName, 1, '.');
          fi;
          Self.SetField(cDataName, nil);
        fi;
      fi;
    od;
    секцТелоСПР.Count = 0;
  end;

  func Получить_ГЕО(rNsp :РКП_Газеты.Газета) :РКП_Справочники.СпрГеография;
    if (rNsp.МестаИздания.Count > 0) then
      Result = rNsp.МестаИздания.Items[1].МестоИздания;
    fi;
  end;

  func Получить_ТЕМ(rNsp :РКП_Газеты.Газета) :РКП_Справочники.спрТематика;
    if (rNsp.Тематики.Count > 0) then
      Result = rNsp.Тематики.Items[1].Тематика;
    fi;
  end;

  func Получить_ЯЗ(rNsp :РКП_Газеты.Газета) :РКП_Справочники.спрЯзыкИздания;
    if (rNsp.Языки.Count > 0) then
      Result = rNsp.Языки.Items[1].Язык;
    fi;
  end;

  func Получить_Дата1(rNsp :РКП_Газеты.Газета) :String;
    if (rNsp.ДатаВыходаПервогоНомера > 01.01.1990) then
      if (rNsp.ТочностьДатыПервогоНомера = 0) then
        Result = Str(rNsp.ДатаВыходаПервогоНомера);
      elsif (rNsp.ТочностьДатыПервогоНомера = 1) then
        Result = РКП_СИС.DateDescription.ToString(rNsp.ДатаВыходаПервогоНомера, РКП_СИС.DateDescription.dfYYYYMMM);
      elsif (rNsp.ТочностьДатыПервогоНомера = 2) then
        Result = Str(Year(rNsp.ДатаВыходаПервогоНомера)) + ' г.';
      fi;
    fi;
  end;

  func Получить_СрРазТираж(rNsp :РКП_Газеты.Газета) :Numeric;
    with Query.Create([РКП_Газеты.ПоступлениеГазеты]) do
      Filter = 'ГодПоступления>0 and Газета=' + Str(rNsp);
      Order = 'ГодПоступления-';
      OpenHint[Query.Packeting] = true;
      PacketSize = 2;
      Select;
      if RecordsExists then
        Result = Current.СреднийРазовыйТираж;
      fi;
    end;
  end;

  func Получить_ЛППИ(rNsp :РКП_Газеты.Газета) :String;
    if (rNsp.ГодЛетописи > 0) then
      Result = Str(rNsp.ГодЛетописи);
      if (rNsp.НомерВЛетописи > 0) then
        Result = Result + ' / ' + Str(rNsp.НомерВЛетописи);
      fi;
    fi;
  end;

  func Получить_ПослНом(rNsp :РКП_Газеты.Газета) :String;
    var rLastIss :РКП_Газеты.НомерГазеты;
    rLastIss = РКП_ГАЗЕТЫ.Стат.ПоследнийПолученныйНомер(rNsp);
    if (rLastIss <> nil) then
      Result = Trim(rLastIss.Номер);
      if (rLastIss.ДатаВыхода > 01.01.1990) then
        Result = Result + ' от ' + Str(rLastIss.ДатаВыхода);
      fi;
    fi;
  end;

  proc ОформитьОтчет;
    var i :Integer;
    var cDataName :String;
    var cColumnVisible, cColumnWidth :Integer;
    var cColumn :TemplateColumn;
    for i = 1 .. секцТелоСПР.ColumnsCount do
      if секцТелоСПР.Column[i].Printed then
        cDataName = секцТелоСПР.Cell[i, 1].Contents;
        if cDataName <> nil then
          if Pos('.', cDataName) > 0 then
            cDataName = ExtractWord(cDataName, 1, '.');
          fi;
          cColumnVisible = Self.ClassType.GetClassField('ФЛ_' + cDataName) as Integer;
          cColumnWidth = Self.ClassType.GetClassField('ШР_' + cDataName) as Integer;
          секцШапкаСПР.Column[i].Visible = (cColumnVisible = 1);
          секцТелоСПР.Column[i].Visible = (cColumnVisible = 1);
          секцШапкаСПР.Column[i].Width = Round(cColumnWidth);
          секцТелоСПР.Column[i].Width = Round(cColumnWidth);
        fi;
      fi;
    od;
  end;


end