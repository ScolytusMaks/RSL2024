class inherited Статьи.редВыпускЛГС_03 "Создание указателя географических объектов", editor ВыпускЛГС;

inclass

inobject
  var У_Гео[]                       : String;
  var У_Тем[]                       : String;
  var У_СтатьяГ[]                   : Integer;
  var У_ОтсылкаГ[]                  : String;
      секцУ_Гео                     : Section;

--------------------------------------------------------------------------------
--{ Обработчики событий бланка
  proc СозданиеУказателяГео;
  var i,k,t     : integer;
  var Q         : Query;
    У_Гео       = nil;
    У_Тем       = nil;
    У_СтатьяГ   = nil;
    У_ОтсылкаГ  = nil;
    --организация запроса к статьям
    i                           = 0;
    Q                           = Query.Create([РКП_Статьи.Роспись]);
    Q.Filter                    = "ГодЛетописи="+Str(ГодЛетописи)+
                                  " and ВыпускЛетописи="+Str(ВыпускЛетописи)+
                                  " and ИндексГео.Count>0";
    Q.LoadingFields             = "ИндексГео;НомерЗаписиВВыпускеГЛ";
    Q.Select;
    --организация цикла
    Q.First;
      for k = 1..Q.Count                                        do
        for t = 1..Q.Current.ИндексГео.Count                    do
          i                     = i + 1;
          У_Гео[i]              = Q.Current.ИндексГео.Items[t].РубрикаГео.Наим;
          У_Тем[i]              = Q.Current.ИндексГео.Items[t].ПодРубрикаСист.КрНаим;
          У_СтатьяГ[i]          = Q.Current.НомерЗаписиВВыпускеГЛ;
          У_ОтсылкаГ[i]         = "";
          if Q.Current.ИндексГео.Items[t].РубрикаГео.ФормаНаим<>nil      then
            i                   = i + 1;
            У_Гео[i]            = Q.Current.ИндексГео.Items[t].РубрикаГео.ФормаНаим;
            У_Тем[i]            = "";
            У_СтатьяГ[i]        = 0;
            У_ОтсылкаГ[i]       = "- см. "+Q.Current.ИндексГео.Items[t].РубрикаГео.Наим;
          end;
        end;
      Q.Next;
      end;
    Q.Close;
    --сортировка и запись в подтаблицу
    секцУ_Гео.Count           = i;
    секцУ_Гео.SortBy("У_Гео,У_Тем,У_СтатьяГ");
    i                         = 0;
    Record.УказательГео.Clear;
    for k = 1..секцУ_Гео.Count                                  do
      if k = 1                                                  then
        i                              = Record.УказательГео.Add;
        УказательГео[i].Гео            = У_Гео[k];
        УказательГео[i].Тематика       = У_Тем[k];
        if У_ОтсылкаГ[k] = nil                                  then
        УказательГео[i].БибЗапись      = Str(У_СтатьяГ[k]);
        else
        УказательГео[i].БибЗапись      = У_ОтсылкаГ[k];
        end;
      else
        if (У_Гео[k] = У_Гео[k-1]) and (У_Тем[k] = У_Тем[k-1]) then
        if У_ОтсылкаГ[k] = nil                                 then
        УказательГео[i].БибЗапись      = УказательГео[i].БибЗапись+", "+Str(У_СтатьяГ[k]);
        end;
        else
        i                              = Record.УказательГео.Add;
        УказательГео[i].Гео            = У_Гео[k];
        УказательГео[i].Тематика       = У_Тем[k];
        if У_ОтсылкаГ[k] = nil                                 then
        УказательГео[i].БибЗапись      = Str(У_СтатьяГ[k]);
        else
        УказательГео[i].БибЗапись      = У_ОтсылкаГ[k];
        end;
        end;
      end;
    end;
  end;--proc СозданиеУказателяГео


--}

end