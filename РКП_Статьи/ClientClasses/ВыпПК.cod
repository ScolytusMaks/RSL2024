class "Выпуск ПК";
Import РКП_СИС Classes StdCode;

inclass
------------------------------------------------------------------------------
  func СоздатьЗапись (ДатаКарточек:date;ЗаписьСтатьи:record)                                : logical;
  --Проверка наличия группы в картотеке "Выпуск ПК"
  --При отсутствии - добавление
    var ГодКарточек     : integer;
    var МесяцКарточек   : integer;
    var ДеньКарточек    : integer;
    var Q1,Q2,Q3        : Query;
    var R1,R2,R3        : record;
    ГодКарточек         = Year(ДатаКарточек);
    МесяцКарточек       = Mon(ДатаКарточек);
    ДеньКарточек        = Day(ДатаКарточек);
      if  ГодКарточек > 1901 and ГодКарточек < 3000                     then
        --группа года
        Q1                      = Query.Create([РКП_Статьи.ВыпускПК]);
        Q1.Filter               = "ОписаниеГруппы='"+Str(ГодКарточек)+"'";
        Q1.Select;
        if Q1.Count = 0                                                 then
          R1                    = РКП_Статьи.ВыпускПК.Create;
          R1.ОписаниеГруппы     = Str(ГодКарточек);
          R1.КодВыпуска         = FixStr(Str(ГодКарточек),"0",4)+       --год
                                  "00"+                                 --месяц
                                  "00"+                                 --день
                                  "00";                                 --00-резерв для доп.выпусков
          R1.isGroup            = True;
          R1.Post;
        else
          Q1.First;
          R1                    = Q1.Current;
        end;
        Q1.Close;
        --группа месяца
        Q2                      = Query.Create([РКП_Статьи.ВыпускПК]);
        Q2.Filter               = "ОписаниеГруппы='"+
                                  FixStr(Str(МесяцКарточек),"0",2)+
                                  " / "+
                                  СИС2.Календарь.ИменаМесяцевИм[МесяцКарточек]+
                                  "' and GroupDoc="+Str(R1);
        Q2.Select;
        if Q2.Count = 0                                                 then
          R2                    = РКП_Статьи.ВыпускПК.Create;
          R2.ОписаниеГруппы     = FixStr(Str(МесяцКарточек),"0",2)+
                                  " / "+
                                  СИС2.Календарь.ИменаМесяцевИм[МесяцКарточек];
          R2.КодВыпуска         = FixStr(Str(ГодКарточек),"0",4)+       --год
                                  FixStr(Str(МесяцКарточек),"0",2)+     --месяц
                                  "00"+                                 --день
                                  "00";                                 --00-резерв для доп.выпусков
          R2.GroupDoc           = R1;
          R2.isGroup            = True;
          R2.Post;
        else
          Q2.First;
          R2                    = Q2.Current;

        end;
        Q2.Close;
        --запись дня
        Q3                      = Query.Create([РКП_Статьи.ВыпускПК]);
        Q3.Filter               = "ДатаВыпуска="+Str(ДатаКарточек)+" and GroupDoc="+Str(R2);
        Q3.Select;
        if Q3.Count = 0                                                 then
          R3                    = РКП_Статьи.ВыпускПК.Create;
          R3.КодВыпуска         = FixStr(Str(ГодКарточек),"0",4)+       --год
                                  FixStr(Str(МесяцКарточек),"0",2)+     --месяц
                                  FixStr(Str(ДеньКарточек),"0",2)+      --день
                                  "00";                                 --00-резерв для доп.выпусков
          R3.ДатаВыпуска        = ДатаКарточек;
          R3.ВыпускЛетописи     = ЗаписьСтатьи.ВыпускЛетописи;
          R3.GroupDoc           = R2;
          R3.Post;
        end;
        Q3.Close;
        Result = true;
      else
        Message("Пожалуйста, проверьте правильность введенного значения!");
        Result = false;
      end;
    Return Result;
  end;
------------------------------------------------------------------------------

inobject

end