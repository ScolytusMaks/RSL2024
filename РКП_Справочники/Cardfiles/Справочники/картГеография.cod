Class inherited Справочники.CardBaseType1 "Места изданий";

import classes Def;

inclass private

  var КлассИнтерфейса :Class RI.Справочники.intBaseType1 = RI.Справочники.География;

  stored var ТипФрагмента :Integer;

inobject private

  var aNewID :Integer[]; -- Массив ID новых записей, созданных/измененных во время жизни экземпляра класса

-- 2_Обработчики событий 0шаблона. --

  proc шаблон_ПриОткрытии(Create :Logical);
    var locColumnTerrEntity: CardFileColumn;
    Запретить_Дублирование = true;
    inherited шаблон_ПриОткрытии(Create);
    --кнПреобразовать.Enabled = false;
    Self.CardFile.TreeRoot = 'Административная подчиненность';
    locColumnTerrEntity = Self.Cardfile.ColumnByField['Категория'];
    UpdateCardFieldList(locColumnTerrEntity, Def.TerrEntity);
  end;

-- 2_Обработчики событий клеток шаблона0. --

  func Поле_Фильтр_ПриНажатии (Cell: TemplateCell; Action: Template.ClickTypes): Logical;
    var r: Integer;
    if (Cell.Contents = 'ТипФрагмента') then
      r = Cell.Row;
      ТипФрагмента = (r - 1);
    else
      Result = Inherited Поле_Фильтр_ПриНажатии(Cell, Action);
      Return; -- чтобы не вызывать дважды "ОбновитьФильтр"
    fi;
    ОбновитьФильтр;
  end;

  func Поле_Фильтр_ПриВыводе (Cell: TemplateCell; Value: Variant; Action: Template.OutputTypes; var Format: String): Variant;
    var r: Integer;
    if (Cell.Contents = 'ТипФрагмента') then
      r = Cell.Row;
      Cell.Caption = if( (r - 1) = ТипФрагмента, 'l', '');
    else
      Result = Inherited Поле_Фильтр_ПриВыводе(Cell, Value, Action, Format);
    fi;
  end;

  proc Поле_Фильтр_ПриВыходе(Cell :TemplateCell; Index :Integer);
    ОбновитьФильтр;
    УстановитьЗаписьПриОткрытии;
  end;


-- 2_Обработчики событий 0картотеки. --

  func картотека_ПриИзменении(Action :Cardfile.ModifyActions; cRecord :Record; Group :Record) :Logical;
    var aRes :Integer[];
    var i :Integer;
    Result = inherited картотека_ПриИзменении(Action, cRecord, Group);
    if (Action = Cardfile.RecordGroupOn) then
      ПолучитьПутьГруппы(cRecord, aRes);
      for i = 1 .. LengthOfArray(aRes) do
        СИС2.Функции.AddInArray(aNewID, aRes[i], true);
      od;
      ОбновитьФильтр;
    fi;
  end;


  func картотека_ПриОткрытииБланка(Action :Integer; newRec :Record) :Logical;
    var aRes :Integer[];
    var i :Integer;
    Result = inherited картотека_ПриОткрытииБланка(Action, newRec);
    ПолучитьПутьГруппы(newRec, aRes);
    for i = 1 .. LengthOfArray(aRes) do
      СИС2.Функции.AddInArray(aNewID, aRes[i], true);
    od;
  end;

  proc ПолучитьПутьГруппы(newRec :Record; var aRes :Integer[]);
    СИС2.Функции.AddInArray(aRes, newRec.DocId, true);
    if (newRec.GroupDoc <> nil) then
      ПолучитьПутьГруппы(newRec.GroupDoc, aRes);
    fi;
  end;

  proc Столбец_ПриРисовании (Column :CardfileColumn; Rec :Record; Selected :Logical; var Color :Integer; Font :Font);
    var R, G, B :Integer;
    inherited Столбец_ПриРисовании (Column, Rec, Selected, Color, Font);
    if ( (Rec as спрГеография).Категория = ктрНеОпределено ) then
      --Font.Bold = true;
      R = 255; G = 0; B = 0;
      Font.Color = (R + G * 256 + B * 256 * 256);
      R = 255; G = 230;  B = 230;
      Color = (R + G * 256 + B * 256 * 256);
    fi;
  end;


-- _Прочие методы. --

  func ПолучитьФильтрКартотеки (var locTreeFilter :String = nil) :String;
    Result = Inherited ПолучитьФильтрКартотеки(locTreeFilter);
    if (Result <> nil) and (aNewID <> nil) then
      Result = РКП_СИС.FilterMaker.MakeFilter([ Result, 'DocId in ' + ToStr(aNewID) ], РКП_СИС.FilterMaker.byOr);
    fi;
    if Result <> nil then
      locTreeFilter = РКП_СИС.FilterMaker.CreateExt([Result]).GetFilterGroups( [РКП_Справочники.спрГеография] );
      Result = РКП_СИС.FilterMaker.MakeFilter([Result, locTreeFilter], РКП_СИС.FilterMaker.byOr);
    fi;
  end;

  func ПолучитьФильтрПоФрагменту :String;
    if (ТипФрагмента = fltByBegin) then
      Result = if((Фрагмент <> nil), 'Match(Наим,"' + Фрагмент +'*")', nil);
    elsif (ТипФрагмента = fltBySubStr) then
      Result = if((Фрагмент <> nil), 'Match(Наим,"*' + Фрагмент +'*")', nil);
    elsif (ТипФрагмента = fltExactMatch) then
      Result = if((Фрагмент <> nil), 'Наим="' + Фрагмент +'"', nil);
    fi;
  end;


end